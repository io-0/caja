plugins {
  id "io.freefair.lombok" version "8.0.1"
  id 'java-library'
  id 'maven-publish'
  id 'com.github.ben-manes.versions' version '0.46.0'
  id 'se.patrikerdes.use-latest-versions' version '0.2.18'
}

group = 'net.io_0'
version = "1.4.2-SNAPSHOT"
sourceCompatibility = 11

repositories {
  mavenCentral()
  maven { url "https://jitpack.io" }
}

dependencies {
  implementation 'ch.qos.logback:logback-classic:1.4.7'
  implementation 'org.ehcache:ehcache:3.10.8'
  implementation 'io.lettuce:lettuce-core:6.2.4.RELEASE'
  implementation 'org.apache.commons:commons-lang3:3.12.0'
  implementation 'com.github.io-0:maja:1.7.1'
  testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
  testImplementation 'org.awaitility:awaitility:4.2.0'
  testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.9.2'
}

test {
  useJUnitPlatform()
}

publishing {
  publications {
    mavenJava(MavenPublication) {
      from components.java
    }
  }
}

// disallow release candidates as upgradable versions from stable
dependencyUpdates.configure {
  def isNonStable = { String version ->
    def stableKeyword = ['RELEASE', 'FINAL', 'GA'].any { String kw -> version.toUpperCase().contains(kw) }
    return !stableKeyword && !(version ==~ /^[0-9,.v-]+(-r)?$/)
  }

  rejectVersionIf {
    isNonStable(it.candidate.version) && !isNonStable(it.currentVersion)
  }
}
